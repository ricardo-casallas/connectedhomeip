# Copyright (c) 2020 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/build.gni")
import("//build_overrides/chip.gni")
import("//build_overrides/efr32_sdk.gni")
import("${build_root}/config/defaults.gni")
import("${efr32_sdk_build_root}/efr32_executable.gni")
import("${efr32_sdk_build_root}/efr32_sdk.gni")

assert(current_os == "freertos")

project_dir = "${chip_root}/examples/minimal"
project_common_dir = "${project_dir}/common"
project_efr32_dir = "${project_dir}/efr32"
examples_plat_dir = "${chip_root}/examples/platform/efr32"

declare_args() {
  # Dump memory usage at link time.
  chip_print_memory_usage = true

  # Monitor & log memory usage at runtime.
  enable_heap_monitoring = false
}

efr32_sdk("sdk") {

  include_dirs = [
    "${project_common_dir}/include",
    "${project_efr32_dir}/include",
    "${examples_plat_dir}",
    "${examples_plat_dir}/${efr32_family}/${efr32_board}",
    "${chip_root}/src/platform/EFR32",
  ]

  defines = [
    "BOARD_ID=${efr32_board}",
    "SL_HEAP_SIZE=(12 * 1024)",
  ]
}

efr32_executable("mini_app") {
  output_name = "chip-example-mini-efr32.out"

  sources = [
    "${examples_plat_dir}/Service.cpp",
    "${examples_plat_dir}/init_efrPlatform.cpp",
    "${examples_plat_dir}/uart.c",
    "src/AppTask.cpp",
    "src/AppTimer.cpp",
    "src/ButtonHandler.cpp",
    "src/ZclCallbacks.cpp",
    "src/main.cpp",
  ]

  deps = [
    ":sdk",
    "${chip_root}/examples/minimal/common",
    "${chip_root}/src/lib",
    "${chip_root}/src/setup_payload",
    "${chip_root}/third_party/openthread/platforms:libopenthread-platform",
    "${chip_root}/third_party/openthread/platforms:libopenthread-platform-utils",
    "${chip_root}/third_party/openthread/repo:libopenthread-cli-ftd",
    "${chip_root}/third_party/openthread/repo:libopenthread-ftd",
  ]

  include_dirs = [
    "./include"
  ]

  defines = [
  ]

  if (enable_heap_monitoring) {
    sources += [ "${examples_plat_dir}/MemMonitoring.cpp" ]
    defines += [ "HEAP_MONITORING" ]
  }

  if (efr32_family == "efr32mg12") {
    ldscript = "${examples_plat_dir}/ldscripts/efr32-MG12P.ld"
  } else if (efr32_family == "efr32mg21") {
    ldscript = "${examples_plat_dir}/ldscripts/efr32-MG21.ld"
  }

  inputs = [ ldscript ]

  ldflags = [
    "-T" + rebase_path(ldscript, root_build_dir)
  ]

  if (chip_print_memory_usage) {
    ldflags += [
      "-Wl,--print-memory-usage",
      "-fstack-usage",
    ]
  }

  output_dir = root_out_dir
}

group("efr32") {
  deps = [ ":mini_app" ]
}

group("default") {
  deps = [ ":efr32" ]
}
