# Copyright (c) 2020 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/build.gni")
import("//build_overrides/chip.gni")
import("//build_overrides/openthread.gni")
import("//build_overrides/efr32_sdk.gni")

import("${chip_root}/build/chip/tools.gni")
import("${build_root}/config/defaults.gni")
import("${efr32_sdk_build_root}/efr32_executable.gni")
import("${efr32_sdk_build_root}/efr32_sdk.gni")

assert(current_os == "freertos")

efr32_project_dir = "${chip_root}/examples/shell/efr32"
examples_plat_dir = "${chip_root}/examples/platform/efr32"

efr32_sdk("sdk") {
  sources = [
    "${efr32_project_dir}/include/CHIPProjectConfig.h",
    "${efr32_project_dir}/include/FreeRTOSConfig.h",
  ]

  include_dirs = [
    "${chip_root}/src/platform/EFR32",
    "${efr32_project_dir}/include",
    "${examples_plat_dir}",
    "${examples_plat_dir}/${efr32_family}/${efr32_board}",
    "${openthread_root}/src/core",
    "${openthread_root}/examples/platforms/efr32/src"
  ]

  defines = [
    "BOARD_ID=${efr32_board}",
    "HAL_VCOM_ENABLE=1",
    "EFR32_LOG_ENABLED=1",
    "SL_HEAP_SIZE=(10 * 1024)",
  ]
}

efr32_executable("chip-shell") {
  output_name = "chip-efr32-shell.out"

  sources = [
    "${examples_plat_dir}/init_efrPlatform.cpp",
    "${examples_plat_dir}/uart.c",
    "src/main.cpp",
  ]

  deps = [
    ":sdk",
    "${chip_root}/src/lib/shell",
    "${chip_root}/src/platform",
    "${chip_root}/examples/shell/shell_common:shell_common",
  ]

#   cflags = [ "-Wconversion" ]
  
  if (efr32_family == "efr32mg12") {
    ldscript = "${examples_plat_dir}/ldscripts/efr32-MG12P.ld"
  } else if (efr32_family == "efr32mg21") {
    ldscript = "${examples_plat_dir}/ldscripts/efr32-MG21.ld"
  }

  inputs = [ ldscript ]

  ldflags = [ "-T" + rebase_path(ldscript, root_build_dir) ]

  output_dir = root_out_dir
}

group("efr32") {
  deps = [ ":chip-shell" ]
}

group("default") {
  deps = [ ":efr32" ]
}
